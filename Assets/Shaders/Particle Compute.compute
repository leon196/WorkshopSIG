#pragma kernel particle

#include "UnityCG.cginc"
#include "common.cginc"

struct ParticleData { float3 position, velocity, seed; };

RWStructuredBuffer<ParticleData> _Particles;

uniform float _Count, _TimeElapsed, _TimeDelta, _Speed, _Friction, _NoiseScale, _BlendCurl, _BlendGravity, _BlendTarget;
uniform float4x4 _MatrixWorld, _MatrixLocal;


[numthreads(8,1,1)]
void particle (uint3 id : SV_DispatchThreadID)
{
	ParticleData particle = _Particles[id.x];
	float3 position = particle.position;
	float3 velocity = particle.velocity;
	float3 target = float3(0,0,0);
	float3 offset = float3(0,0,0);

	// Curl 
	float3 seed = position * _NoiseScale + particle.seed;
	rotation2D(seed.xz, _TimeElapsed*.01);
	rotation2D(seed.yz, _TimeElapsed*.05);
	float salty = noiseIQ(seed)*2.-1.;
	float sweety = noiseIQ(seed+float3(1.64,5.579,9.5468))*2.-1.;
	float curry = noiseIQ(seed+float3(524.5687,95.546,481.5498))*2.-1.;
	offset += float3(salty, curry, sweety) * _BlendCurl;

	// Gravity
	offset.y += _BlendGravity;

	// Go to target
	float l = length(target-position);
	if (l > .01) {
		offset += normalize(target-position) * smoothstep(.0, .1, l) * _BlendTarget;
	}

	velocity = velocity * _Friction + offset * _Speed * _TimeDelta;
	position += velocity;

	_Particles[id.x].velocity = velocity;
	_Particles[id.x].position = position;
}
